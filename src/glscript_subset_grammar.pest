// GlScript subset tokenizer grammar
SourceFile  =  { _expression* }
_expression = _{ _tracked | Untracked }

_tracked  = _{ _includeStatement | _region | Regex | _stringLiteral | _comment | LineTerminator }
Untracked =  { (!(_trackedStart | PlaceholderCloseBracket) ~ ANY)+ | (!(_trackedStart) ~ ANY) }
// .                              ^fix nested stmt

_trackedStart = _{
    "import"
  | "#include"
  | "#text"
  | "#sql"
  | "#end"
  | "\""
  | "'"
  | "`"
  | "/"
  | "\n"
  | "\r"
}

LineTerminator    =  { "\n" | "\r\n" | "\r" }
WhiteSpace        =  { (" " | "\t")+ }
_maybeEscapedChar = _{ ("\\" ~ ANY) | (!"\\" ~ ANY) }

// Include statement
_includeStatement = _{ IncludeToken ~ (WhiteSpace | LineTerminator | _comment)+ ~ IncludePath }
IncludeToken      = @{ "import" | "#include" }
IncludePath       = @{ DoubleStringLiteral | SingleStringLiteral | ("<" ~ (!(">" | LineTerminator) ~ ANY)* ~ ">") }

// Regions
RegionOpen                = @{ _regionOpenUntrackedMark ~ _regionOpenToken ~ _regionOpenUntrackedMark }
RegionChars               = @{ (!(RegionClose | LineTerminator) ~ ANY)+ }
RegionClose               = @{ _regionCloseUntrackedMark ~ _regionCloseToken ~ _regionCloseUntrackedMark }
_regionOpenToken          = _{ "#" ~ ("text" | "sql") }
_regionOpenUntrackedMark  = _{ (!(LineTerminator | _regionOpenToken) ~ ANY)* }
_regionCloseToken         = _{ "#end" ~ ("text" | "sql") }
_regionCloseUntrackedMark = _{ (!(LineTerminator | _regionCloseToken) ~ ANY)* }
_region                   = _{
    RegionOpen ~ (LineTerminator | RegionChars)+ ~ RegionClose
}

// String literals
_stringLiteral = _{ DoubleStringLiteral | SingleStringLiteral | _templateString }

DoubleStringLiteral = @{ "\"" ~ (!("\"" | LineTerminator) ~ _maybeEscapedChar)* ~ "\"" }
SingleStringLiteral = @{ "'" ~ (!("'" | LineTerminator) ~ _maybeEscapedChar)* ~ "'" }

TemplateStringBracket   = @{ "`" }
TemplateStringChars     = @{ (!(TemplateStringBracket | PlaceholderOpenBracket | LineTerminator) ~ _maybeEscapedChar)+ }
PlaceholderOpenBracket  = @{ "${" }
PlaceholderCloseBracket = @{ "}" }
_templateString         = _{ TemplateStringBracket ~ _templatePart* ~ TemplateStringBracket }
_templatePart           = _{ _templateExpression | LineTerminator | TemplateStringChars }
_templateExpression     = _{
    PlaceholderOpenBracket ~ (!PlaceholderCloseBracket ~ _expression)+ ~ PlaceholderCloseBracket
}

// RegExp
Regex         = @{ _regexBracket ~ _regexChars ~ _regexBracket }
_regexChars   = _{ (!(_regexBracket | LineTerminator) ~ _maybeEscapedChar)+ }
_regexBracket = _{ (!("\\/" | MultiLineCommentOpenBracket) ~ "/") }

// Comments
_comment = _{ SingleLineComment | _muliLineComment }

SingleLineComment = @{ "//" ~ (!LineTerminator ~ ANY)* }

MultiLineCommentOpenBracket  = @{ "/*" }
MultiLineCommentChars        = @{ (!(MultiLineCommentCloseBracket | LineTerminator) ~ _maybeEscapedChar)+ }
MultiLineCommentCloseBracket = @{ "*/" }
_muliLineComment             = _{
    MultiLineCommentOpenBracket ~ (LineTerminator | MultiLineCommentChars)+ ~ MultiLineCommentCloseBracket
}
