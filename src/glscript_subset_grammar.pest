// GlScript subset tokenizer grammar
SourceFile     = { _expression* }
SourceFileFast = { _expressionFast* }

_expression     = _{ _tracked | Common }
_expressionFast = _{ _trackedFast | CommonFast }

_tracked     = _{ _includeStatement | _region | Regex | _stringLiteral | _comment | LineTerminator }
_trackedFast = _{ _includeStatement | _region | Regex | _stringLiteralFast | _comment | CommonWithLineEnding | LineTerminator }

Common = { (!(_tracked | PlaceholderCloseBracket) ~ ANY)+ | (!(_tracked) ~ ANY) }
// .                     ^fix nested stmt

CommonFast = { (!(_trackedStart | PlaceholderCloseBracket) ~ ANY)+ | (!(_trackedStart) ~ ANY) }

CommonWithLineEnding = @{ CommonFast ~ LineTerminator }

_trackedStart = _{
    (IncludeToken ~ WhiteSpace)
  | _regionOpenToken
  | SingleLineCommentOpenToken
  | MultiLineCommentOpenBracket
  | Regex
  | LineTerminator
  | _doubleQouteBracket
  | _singleQouteBracket
  | TemplateStringBracket
}

LineTerminator    =  { "\n" | "\r\n" | "\r" }
WhiteSpace        =  { (" " | "\t")+ }
_maybeEscapedChar = _{ ("\\" ~ ANY) | (!"\\" ~ ANY) }

// Include statement
_includeStatement = _{ IncludeToken ~ (WhiteSpace | LineTerminator | _comment)+ ~ IncludePath }
IncludeToken      = @{ "import" | "#include" }
IncludePath       = @{ DoubleStringLiteral | SingleStringLiteral | ("<" ~ (!(">" | LineTerminator) ~ ANY)* ~ ">") }

// Regions
RegionOpen                = @{ _regionOpenUntrackedMark ~ _regionOpenToken ~ _regionOpenUntrackedMark }
RegionChars               = @{ (!(RegionClose | LineTerminator) ~ ANY)+ }
RegionClose               = @{ _regionCloseUntrackedMark ~ _regionCloseToken ~ _regionCloseUntrackedMark }
_regionOpenToken          = _{ "#" ~ ("text" | "sql") }
_regionOpenUntrackedMark  = _{ (!(LineTerminator | _regionOpenToken) ~ ANY)* }
_regionCloseToken         = _{ "#end" ~ ("text" | "sql") }
_regionCloseUntrackedMark = _{ (!(LineTerminator | _regionCloseToken) ~ ANY)* }
_region                   = _{
    RegionOpen ~ (LineTerminator | RegionChars)+ ~ RegionClose
}

// String literals
_stringLiteral     = _{ DoubleStringLiteral | SingleStringLiteral | _templateString }
_stringLiteralFast = _{ DoubleStringLiteral | SingleStringLiteral | _templateStringFast }

_doubleQouteBracket =  { "\"" }
_singleQouteBracket =  { "'" }
DoubleStringLiteral = @{ _doubleQouteBracket ~ (!(_doubleQouteBracket | LineTerminator) ~ _maybeEscapedChar)* ~ _doubleQouteBracket }
SingleStringLiteral = @{ _singleQouteBracket ~ (!(_singleQouteBracket | LineTerminator) ~ _maybeEscapedChar)* ~ _singleQouteBracket }

TemplateStringBracket   = @{ "`" }
TemplateStringChars     = @{ (!(TemplateStringBracket | PlaceholderOpenBracket | LineTerminator) ~ _maybeEscapedChar)+ }
PlaceholderOpenBracket  = @{ "${" }
PlaceholderCloseBracket = @{ "}" }
_templateString         = _{ TemplateStringBracket ~ _templatePart* ~ TemplateStringBracket }
_templatePart           = _{ _templateExpression | LineTerminator | TemplateStringChars }
_templateExpression     = _{
    PlaceholderOpenBracket ~ (!PlaceholderCloseBracket ~ _expression)+ ~ PlaceholderCloseBracket
}

_templateExpressionFast = _{
    PlaceholderOpenBracket ~ (!PlaceholderCloseBracket ~ _expressionFast)+ ~ PlaceholderCloseBracket
}
_templatePartFast       = _{ _templateExpressionFast | LineTerminator | TemplateStringChars }
_templateStringFast     = _{ TemplateStringBracket ~ _templatePartFast* ~ TemplateStringBracket }

// RegExp
Regex         = @{ _regexBracket ~ _regexChars ~ _regexBracket }
_regexChars   = _{ (!(_regexBracket | LineTerminator) ~ _maybeEscapedChar)+ }
_regexBracket = _{ (!("\\/" | MultiLineCommentOpenBracket) ~ "/") }

// Comments
_comment = _{ SingleLineComment | _muliLineComment }

SingleLineComment          = @{ SingleLineCommentOpenToken ~ (!LineTerminator ~ ANY)* }
SingleLineCommentOpenToken =  { "//" }

MultiLineCommentOpenBracket  = @{ "/*" }
MultiLineCommentChars        = @{ (!(MultiLineCommentCloseBracket | LineTerminator) ~ _maybeEscapedChar)+ }
MultiLineCommentCloseBracket = @{ "*/" }
_muliLineComment             = _{
    MultiLineCommentOpenBracket ~ (LineTerminator | MultiLineCommentChars)+ ~ MultiLineCommentCloseBracket
}
